# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=1.4.0
pkgrel=3

pkgdesc="Contributed scripts and tools for pacman systems"
url="https://git.archlinux.org/pacman-contrib.git/about/"
arch=('x86_64')
license=('GPL')

depends=('fakeroot' 'pacman' 'perl')
makedepends=('asciidoc')
optdepends=('diffutils: for pacdiff'
            'findutils: for pacdiff --find'
            'mlocate: for pacdiff --locate'
            'sudo: privilege elevation for several scripts'
            'vim: default merge program for pacdiff')

source=("https://git.archlinux.org/pacman-contrib.git/snapshot/pacman-contrib-$pkgver.tar".{gz,asc}
        '0001-pactree-fix-compilation-with-pacman-6.patch')
sha256sums=('8746f1352aaad990fe633be34f925d5ab8bd8a19a5f1274e72dbde426deb86bd'
            'SKIP'
            '774d27532a91e2fe490ccc8d21c2d1d4d2a2dbfc8678a8406abb8bb8f9e6626c')
validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5'  # Johannes Löthberg <johannes@kyriasis.com>
              '04DC3FB1445FECA813C27EFAEA4F7B321A906AD9') # Daniel M. Capella <polyzen@archlinux.org>

prepare() {
  cd pacman-contrib-$pkgver

  patch --forward --strip=1 --input=../0001-pactree-fix-compilation-with-pacman-6.patch
  ./autogen.sh
}

build() {
  cd pacman-contrib-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  make -C pacman-contrib-$pkgver check
}

package() {
  cd pacman-contrib-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
