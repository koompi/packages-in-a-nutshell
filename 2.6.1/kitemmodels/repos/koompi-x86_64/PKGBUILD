# Maintainer: Hangsia Hong <hangsiahong@koompi.org>
# Maintainer: Hangsia Hong <hangsiahong@koompi.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kitemmodels
pkgver=5.83.0
pkgrel=1
pkgdesc='Models for Qt Model/View system'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules python-pyqt5 clang doxygen qt5-tools qt5-doc sip4 qt5-declarative)
optdepends=('python-pyqt5: for the Python bindings' 'qt5-declarative: QML bindings')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('650deaf788fc98c9c40e3c26a2462dbe688fb104dd73fa64b21ee9511eb6d2cf'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
