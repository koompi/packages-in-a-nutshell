# Maintainer: Hangsia Hong <hangsiahong@koompi.org>
# Contributor: Hangsia Hong <hangsiahong@koompi.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgbase=lapack
pkgname=(lapack blas cblas lapacke lapack-doc)
pkgver=3.9.1
pkgrel=1
url="https://www.netlib.org/lapack"
pkgdesc="Linear Algebra PACKage"
makedepends=(gcc-fortran cmake python doxygen)
arch=(x86_64)
license=(custom)
source=($pkgbase-$pkgver.tar.gz::"https://github.com/Reference-LAPACK/lapack/archive/v$pkgver.tar.gz" LICENSE.blas)
sha256sums=('d0085d2caf997ff39299c05d4bacb6f3d27001d25a4cc613d48c1f352b73e7e0'
            '2420cd1331c5754071966389b26e6196c5cb086a2ade0080953af62e0c6d4107')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE_WITH_TMG=ON \
    -DCBLAS=ON \
    -DBUILD_DEPRECATED=ON
  cmake --build build

# Build man pages
  cd $pkgname-$pkgver
  doxygen DOCS/Doxyfile_man
}

package_lapack() {
  depends=(blas)
  
  DESTDIR="$pkgdir" cmake --install build

  rm -r "$pkgdir"/usr/lib/{libblas.*,libcblas.*,liblapacke.*}
  rm -r "$pkgdir"/usr/lib/pkgconfig/{blas.*,cblas.*,lapacke.*}
  rm -r "$pkgdir"/usr/lib/cmake/{cblas*,lapacke*}
  rm -r "$pkgdir"/usr/include
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=(gcc-libs)

  DESTDIR="$pkgdir" cmake --install build/BLAS
  
  install -Dm644 LICENSE.blas -t "$pkgdir"/usr/share/licenses/blas/
}

package_cblas() {
  pkgdesc="C interface to BLAS"
  depends=(blas)

  DESTDIR="$pkgdir" cmake --install build/CBLAS
}

package_lapacke() {
  pkgdesc="C interface to LAPACK"
  depends=(lapack)

  DESTDIR="$pkgdir" cmake --install build/LAPACKE
}

package_lapack-doc() {
  pkgdesc="Man pages for BLAS/LAPACK"

  mkdir -p "$pkgdir"/usr/share
  cp -r lapack-$pkgver/DOCS/man "$pkgdir"/usr/share
}
